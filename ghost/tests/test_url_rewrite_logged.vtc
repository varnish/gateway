varnishtest "URL rewrite with logging"

server s1 {
    timeout 3
    rxreq
    expect req.url == "/api/v2/users"
    txresp -body "OK"
} -start

shell {
    cat > ${tmpdir}/ghost.json <<EOF
{
    "version": 2,
    "vhosts": {
        "test.example.com": {
            "routes": [
                {
                    "path_match": {
                        "type": "PathPrefix",
                        "value": "/api/v1"
                    },
                    "backends": [
                        {"address": "${s1_addr}", "port": ${s1_port}, "weight": 100}
                    ],
                    "filters": {
                        "url_rewrite": {
                            "path_type": "ReplacePrefixMatch",
                            "replace_prefix_match": "/api/v2"
                        }
                    },
                    "priority": 100
                }
            ]
        }
    }
}
EOF
}

varnish v1 -arg "-p thread_pool_stack=160k" -vcl {
    import ghost from "${vmod}";

    backend dummy none;

    sub vcl_init {
        ghost.init("${tmpdir}/ghost.json");
        new router = ghost.ghost_backend();
    }

    sub vcl_recv {
        if (req.url == "/.varnish-ghost/reload") {
            if (router.reload()) {
                return (synth(200, "OK"));
            } else {
                return (synth(500, "Reload failed"));
            }
        }
    }

    sub vcl_backend_fetch {
        set bereq.backend = router.backend();
    }
} -start

logexpect l1 -v v1 -g raw {
    expect * * Debug "Applying URL rewrite filter"
    expect * * Debug "ReplacePrefixMatch:.*"
} -start

client c_reload {
    timeout 3
    txreq -url "/.varnish-ghost/reload"
    rxresp
    expect resp.status == 200
} -run

client c1 {
    timeout 3
    txreq -url "/api/v1/users" -hdr "Host: test.example.com"
    rxresp
    expect resp.status == 200
} -run

logexpect l1 -wait
